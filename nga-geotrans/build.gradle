apply plugin: 'com.android.library'

//apply from: "$rootDir/build-support/emp3-gradle-plugin/src/main/resources/android-lib.gradle"
apply plugin: 'nebula.maven-base-publish'
apply plugin: 'nebula.maven-nebula-publish'

android {
    compileSdkVersion  23
    buildToolsVersion "26.0.2"
    defaultConfig {
        versionCode 1
        versionName "3.0.0"
        minSdkVersion 19
        targetSdkVersion   23
    }

    sourceSets {
        main {
            //jni.srcDirs = ['src/main/jni']
            jni.srcDirs = ['src/main/java']
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }
    externalNativeBuild {
        ndkBuild {
            // Application.mk in the same directory will be used automatically
            path 'src/main/jni/Android.mk'
        }
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url "file://${System.env['ANDROID_HOME']}/extras/android/m2repository"
    }
    maven {
        url 'https://maven.google.com'
        // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
    }
    maven {
        url 'https://bintray.com/nasaworldwind/maven/WorldWindAndroid/'
    }
    maven {
        url "https://dl.bintray.com/missioncommand/maven/"
    }
    maven {
        url "https://oss.jfrog.org/artifactory/libs-snapshot"
    }
}

dependencies {
    implementation fileTree (dir: 'libs', include: ['*.jar'])
    testImplementation      ("junit:junit:4.12")
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("build/javadoc/")
    failOnError false
}


task javadocZip(type: Zip) {
    dependsOn javadoc
    from 'build/javadoc/'
    include '*'
    include '*/*' //to include contents of a folder present inside Reports directory
    archiveName 'geotrans-javadoc.zip'
    destinationDir(file('build/outputs/libs'))
}

publishing.publications {
    nebula(MavenPublication) {
        artifactId = "nga-geotrans"

        artifact("$buildDir/outputs/aar/nga-geotrans-release.aar") {
            extension "aar"
        }

//        artifact("$buildDir/outputs/libs/geotrans-javadoc.zip") {
//            extension "zip"
//        }
    }
}
